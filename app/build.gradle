apply plugin: 'com.android.application'
apply plugin: 'deploygate'
apply plugin: 'me.tatarka.retrolambda'

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "hm.orz.chaos114.android.carnavimodoki"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            if (System.getenv('CIRCLECI')) {
                storeFile=file(System.getenv('STORE_FILE'))
                storePassword=System.getenv('STORE_PASSWORD')
                keyAlias=System.getenv('KEY_ALIAS')
                keyPassword=System.getenv('KEY_PASSWORD')
            } else {
                storeFile=file(project.properties.storeFile)
                storePassword=project.properties.storePassword
                keyAlias=project.properties.keyAlias
                keyPassword=project.properties.keyPassword
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.michaelpardo:activeandroid:3.1.0-SNAPSHOT'
    provided "org.projectlombok:lombok:1.16.2"
    compile 'com.squareup:otto:1.3.6'
    compile 'com.github.operando:garum:0.0.2'

    debugCompile 'com.facebook.stetho:stetho:1.0.0'
}

// Using Google Play Services causes retrolambda to fail
retrolambda {
    jvmArgs '-noverify'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.0.1'
    }
}

deploygate {
    userName = "$System.env.DEPLOYGATE_USER"
    token = "$System.env.DEPLOYGATE_TOKEN"

    apks {
        debug {
            sourceFile = file("${project.buildDir}/outputs/apk/app-debug.apk")
        }
        release {
            sourceFile = file("${project.buildDir}/outputs/apk/app-release.apk")
        }
    }
}

uploadDeployGateDebug.dependsOn assembleDebug
uploadDeployGateRelease.dependsOn assembleDebug
